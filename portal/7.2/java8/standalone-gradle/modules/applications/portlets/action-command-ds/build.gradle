/*
 *  Apply Liferay Gradle plugin which will 
 *  - Instruct Bnd to use portalble contracts instead of package dependencies, when possible
 *  - Add a Bnd plugin to analyze the JSPs
 *  - Add a Bnd plugin to analyze i18n resource bundles
 *  - Add a Bnd plugin to analyze SASS/CSS files
 */
apply plugin: "com.liferay.plugin"				

/*
 * Spring's dependency plugin is needed to provide `dependencyManagement` functionality
 * used to configure Liferay Portal's BOM (Biil Of Material)
 */
apply plugin: "io.spring.dependency-management"


/*
 * Configure plugins 
 */
buildscript {
	dependencies {
		classpath "biz.aQute.bnd:biz.aQute.bnd:4.2.0"
		classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:4.2.0"
		classpath "biz.aQute.bnd:biz.aQute.bndlib:4.2.0"
		classpath(group: "com.liferay", name: "com.liferay.gradle.plugins", version: "4.2.0") {
			exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
			exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd.gradle"
			exclude group: "biz.aQute.bnd", module: "biz.aQute.bndlib"
		}
		classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
			url "https://plugins.gradle.org/m2/"
		}
	}
}

/*
 * Repositories for project depenedencies
 */
repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

/* 
 * Use the BOM provided for given Liferay Portal version
 * This allows to automatically set proper versions for dependencies
 */
dependencyManagement {
    imports {
        mavenBom("com.liferay.portal:release.portal.bom:7.2.0")
    }
}

/* 
 * Use the BOM provided for given Liferay Portal version
 * This allows to automatically set proper versions for dependencies
 */
dependencies {
	compileOnly "com.liferay.portal:com.liferay.portal.kernel"	// version will be taken from BOM 
	compileOnly "com.liferay.portal:com.liferay.util.taglib"	// version will be taken from BOM 
	compileOnly "javax.portlet:portlet-api:3.0.0"
	compileOnly "javax.servlet:javax.servlet-api:3.0.1"
	compileOnly "org.osgi:org.osgi.service.component.annotations:1.3.0"
}

/*
 * Set the jar name to ensure it's the same accross all builds
 */
jar.baseName = 'com.liferay.code.samples.portal.modules.applications.portlets.action-command-ds'